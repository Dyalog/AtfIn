 r←ToDyalog(sourcedir targetdir sourceapl);files;file;src;APLtype;new;parts;map;z;dest;rewrites
⍝ Convert a folder containing APL source files (.aplf and .apla) to Dyalog APL
⍝ From APL interpreter indentified by sourceapl

 :If ~⎕NEXISTS sourcedir
 :OrIf 1≠⊃1 ⎕NINFO sourcedir
     →0⊣r←'Not a directory: ',sourcedir
 :EndIf

 3 ⎕MKDIR targetdir
 ⎕NDELETE⍠1⊢targetdir,'/*.apl?'

 :If (≢∆APLs)<APLtype←∆APLs⍳⍥⎕C⊂sourceapl
     →0⊣r←'sourceapl must be one of: ',⍕∆APLs
 :EndIf

 :If ⎕NEXISTS map←sourcedir,'/atfmap.txt'
     ⎕←'Using ',map
     z←1⊃⎕NGET map 1
     ('Invalid map file for source apl = ',sourceapl)⎕SIGNAL(sourceapl''≢⍥⎕C⊢2↑z)↓11
     ∆⍙CodT←↑,/'%',¨2↓z
 :Else
     ∆⍙CodT←DefTranslation 1 ⎕C sourceapl
 :EndIf

 files←'/*'~⍥{⊃⎕NINFO⍠1⊢sourcedir,⍵}'/atfmap.txt'
 rewrites←⊃⎕NINFO⍠1⊢targetdir,'/rewrite/*'
 r←(⍕≢files),' files processed (',(⍕≢rewrites),' were replaced by rewrites)'
 files/⍨←~files∊⍥(1↓¨⎕NPARTS)rewrites

 ⍞←'Progress: '
 :For file :In files
     src←⊃⎕NGET file 0
     parts←⎕NPARTS file
     dest←targetdir,'/',∊1↓parts
     :Select 3⊃parts
     :Case '.aplf'
         src←∆fixrep src
         (⊂src)⎕NPUT dest
         ⍞←'f'
     :Case '.apla'
         (⊂src)⎕NPUT dest
         ⍞←'a'
     :Else
         dest ⎕NCOPY file
         ⍞←'?'
     :EndSelect
 :EndFor
