 rewrites←{files}ToDyalog(sourcedir targetdir sourceapl);files;file;src;APLtype;new;parts;map;z;dest;rewrote;⎕ML
⍝ Convert a folder containing APL source files (.aplf and .apla) to Dyalog APL
⍝ From APL interpreter indentified by sourceapl

 ⎕ML←1
 :If ~⎕NEXISTS sourcedir
 :OrIf 1≠⊃1 ⎕NINFO sourcedir
     →0⊣r←'Not a directory: ',sourcedir
 :EndIf
 3 ⎕MKDIR targetdir
 :If 0=⎕NC'files'
     ⎕NDELETE⍠1⊢targetdir,'/*.apl?'
 :EndIf

 :If (≢∆APLs)<APLtype←∆APLs⍳⍥⎕C⊂sourceapl
     →0⊣r←'sourceapl must be one of: ',⍕∆APLs
 :EndIf

 :If ⎕NEXISTS map←sourcedir,'/atfmap.txt'
     ⎕←'Using ',map
     z←1⊃⎕NGET map 1
     ('Invalid map file for source apl = ',sourceapl)⎕SIGNAL(sourceapl''≢⍥⎕C⊢2↑z)↓11
     ∆⍙CodT←↑,/'%',¨2↓z
 :Else
     ∆⍙CodT←DefTranslation 1 ⎕C sourceapl
 :EndIf

 :If 0=⎕NC'files'
     files←'/*'~⍥{⊃⎕NINFO⍠1⊢sourcedir,⍵}'/atfmap.txt'
 :Else
     files←(sourcedir,'/',∊)¨1↓¨⎕NPARTS files
 :EndIf
 rewrites←⊃⎕NINFO⍠1⊢targetdir,'/rewrite/*'
 rewrote←files∊⍥(1↓¨⎕NPARTS)rewrites
 rewrites←rewrote/files
 files/⍨←~rewrote

 ⍞←'Progress: '
 :For file :In files
     src←⊃⎕NGET file 0
     parts←⎕NPARTS file
     dest←targetdir,'/',∊1↓parts
     :Select 3⊃parts
     :Case '.aplf'
         :Trap 0
            src←∆fixrep src
            (⊂src)⎕NPUT dest 1
            ⍞←'f'
         :Else
            ⎕←'failed to process "',file,'": ',,⍕⎕DMX.DM
         :EndTrap
     :Case '.apla'
         (⊂src)⎕NPUT dest 1
         ⍞←'a'
     :Else
         dest ⎕NCOPY⍠'IfExists' 'Replace'⊢file
         ⍞←'?'
     :EndSelect
 :EndFor
 ⍞←(⎕UCS 13),(⍕≢files),' files processed ─ ',(⍕+/rewrote),' were replaced by rewrites'
